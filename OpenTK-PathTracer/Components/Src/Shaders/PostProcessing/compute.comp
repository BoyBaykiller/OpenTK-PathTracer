#version 430 core
layout (local_size_x = 32, local_size_y = 1, local_size_z = 1) in;

layout(binding = 0, rgba8) restrict writeonly uniform image2D ImgResult;
layout(binding = 1) uniform sampler2D Sampler0;
layout(binding = 2) uniform sampler2D Sampler1;
layout(binding = 3) uniform sampler2D Sampler2;

vec3 LessThan(vec3 f, float value);
vec3 InverseGamma(vec3 rgb);
vec3 ACESFilm(vec3 x);

void main()
{
    ivec2 imgResultSize = imageSize(ImgResult);
    ivec2 imgCoord = ivec2(gl_GlobalInvocationID.x / imgResultSize.y, gl_GlobalInvocationID.x % imgResultSize.y);
    vec2 texCoord = imgCoord / vec2(imgResultSize);
    
    vec3 color = texture(Sampler0, texCoord).rgb;
    color += texture(Sampler1, texCoord).rgb;
    //color += texture(Sampler2, texCoord).rgb;
    
    color = ACESFilm(color);
    color = InverseGamma(color);
    
    imageStore(ImgResult, imgCoord, vec4(color, 1.0));
}

vec3 LessThan(vec3 f, float value)
{
    return vec3(
        (f.x < value) ? 1.0 : 0.0,
        (f.y < value) ? 1.0 : 0.0,
        (f.z < value) ? 1.0 : 0.0);
}

vec3 InverseGamma(vec3 rgb)
{
    //rgb = clamp(rgb, 0.0, 1.0);
    return mix(
        pow(rgb, vec3(1.0 / 2.4)) * 1.055 - 0.055,
        rgb * 12.92,
        LessThan(rgb, 0.0031308)
    );
}
 
// ACES tone mapping curve fit to go from HDR to LDR
// https://knarkowicz.wordpress.com/2016/01/06/aces-filmic-tone-mapping-curve/
vec3 ACESFilm(vec3 x)
{
    float a = 2.51;
    float b = 0.03;
    float c = 2.43;
    float d = 0.59;
    float e = 0.14;
    return clamp((x * (a * x + b)) / (x * (c * x + d) + e), 0.0, 1.0);
}